//-------------------------------------------------------//
// nccx.mac -- 穴数カウントマクロ Ver0.21                //
//                                                       //
// Author:  WATABE Eiji                                  //
// Created: Apr 26, 2000                                 //
// Revised: Apr 30, 2001                                 //
//-------------------------------------------------------//

// 田楽DLLのロード
freedll;
loaddll(hidemarudir + "\\DengakuDLL.dll");
if (!result) {
	message "DLL をロードできませんでした。";
	endmacro;
}
//$format = "left"; // 穴数を左寄せで表示
$format = "right"; // 穴数を右寄せで表示
$AnaKazu = "Tool\t 穴数\n----\t -------\n";

disabledraw;
// 現在のカーソル位置を記憶
#x = x;
#y = y;
if (selecting == 1) {
	$nc = getenv("temp") + "\\ncc.$nc";
	copy;
	#handle1 = hidemaruhandle(0);
	openfile "/h " + $nc;
	#handle2 = hidemaruhandle(0);
	paste;
	gofileend;
	searchup "^T[0-9]+$", casesense, regular;
	if (result == 0) {
		gofiletop;
		golinetop;
		insert "T??\n";
	}
	config "m-";
	save;
	call Count;
	setactivehidemaru #handle1;
	closehidemaru #handle2;
	if (!dllfunc("REMOVE", "/f " + $nc)) {
		message "テンポラリファイルの削除に失敗しました";
	}
} else {
	call Count;
}
call addcom str(#Total);
call column $$return;

$AnaKazu = $AnaKazu + "----\t -------\n合計\t" + $$return + "\n";
// カーソル位置を元に戻す
moveto #x, #y;
enabledraw;
message $AnaKazu;
freedll;
endmacro;

//-------------------------------------------------------//
// 穴数のカウント                                        //
//-------------------------------------------------------//
Count:
gofiletop;
golinetop;
searchdown "^N(4[4-9]|[5-8][0-9]|9[0-7])$", regular;
while (result == true) {
	$line = gettext(0, y, linelen2, y);
	#SubNum = val(rightstr($line, strlen($line) - 1));
	call Sub;
	#N[#SubNum] = ##return;
	searchdown "^N(4[4-9]|[5-8][0-9]|9[0-7])$", regular;
}
gofiletop;
golineend;
searchup "^T[?0-9]+$", casesense, regular;
if (result == 0) searchdown "^T[?0-9]+$", regular;
while (result == true) {
	$line = gettext(0, y, linelen2, y);
	call Main;
	if ($line == "T50") {
		call addcom str(##return);
		call column "(" + $$return + ")";
	} else {
		#Total = #Total + ##return;
		call addcom str(##return);
		call column $$return;
	}
	$Tool = $$return;
        if ($line == "T??") $line = "";
	$AnaKazu = $AnaKazu + $line + "\t" + $Tool + "\n";
	searchdown "^T[?0-9]+$", regular;
}
return;

//-------------------------------------------------------//
// サブメモリ部のカウント                                //
//-------------------------------------------------------//
Sub:
while (1) {
	down;
        if (!result) break;
	$$line = gettext(0, y, linelen2, y);
	if (strstr($$line, "M99") > -1)
		break;
	else if (strstr($$line, "G81") > -1) {
		while (1) {
			down;
		        if (!result) break;
			$$line = gettext(0, y, linelen2, y);
			if (strstr($$line, "X") > -1 || strstr($$line, "Y") > -1)
				##i = ##i + 1;
			else if ($$line == "G80")
				break;
		}
	}
}
return ##i;

//-------------------------------------------------------//
// メインメモリ部のカウント                              //
//-------------------------------------------------------//
Main:
while (1) {
	down;
        if (!result) break;
	$$line = gettext(0, y, linelen2, y);
	if (strstr($$line, "T") > -1)
		break;
	else if (strstr($$line, "M02") > -1)
		break;
	else if (strstr($$line, "M89") > -1)
		##j = ##j + 1;
	else if (strstr($$line, "M") > -1) {
		##i = val(rightstr($$line, strlen($$line) - 1));
		##j = ##j + #N[##i];
	} else if ($$line == "G81") {
		while (1) {
			down;
		        if (!result) break;
			$$line = gettext(0, y, linelen2, y);
			if (strstr($$line, "X") > -1 || strstr($$line, "Y") > -1)
				##j = ##j + 1;
			else if ($$line == "G80")
				break;
		}
	}
}
up;
return ##j;

//-------------------------------------------------------//
// 千の位に","を打つ                                     //
//-------------------------------------------------------//
addcom:
while (strlen($$1) > 3) {
	$$str = "," + rightstr($$1, 3) + $$str;
	$$1 = leftstr($$1, strlen($$1) - 3);
}
return $$1 + $$str;

//-------------------------------------------------------//
// 桁を揃える                                            //
//-------------------------------------------------------//
column:
##n = 8; // 桁数
if ($format == "right")
	if (strstr($$1, ")") == -1) {
		while (strlen($$1) < ##n) {
			$$1 = " " + $$1;
		}
	} else {
		while (strlen($$1) < ##n + 1) {
			$$1 = " " + $$1;
		}
} else {
	if (strstr($$1, ")") == -1)
		$$1 = " " + $$1;
}
return $$1;
