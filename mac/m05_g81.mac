//-------------------------------------------------------//
// m05_g81.mac -- M05/M07 Hit → CR Hit Convert Macro    //
// Ver0.1                                                //
//                                                       //
// Author:  WATABE Eiji                                  //
// Created: May 25, 2001                                 //
// Revised: May 25, 2001                                 //
//-------------------------------------------------------//

// 田楽DLLのロード
freedll;
loaddll(hidemarudir + "\\DengakuDLL.dll");
if (!result) {
	message "DLL をロードできませんでした。";
	endmacro;
}

// 現在のカーソル位置を記憶
#x = x;
#y = y;
disabledraw;

// 書式を "X〜Y〜M05" 形式にする
replaceallfast "\nM05", "M05", regular;
replaceallfast "\nM07", "M07", regular;

gofileend;
#Total = lineno;
gofiletop;
golinetop;
#HitFlag = 0;
#Count = 1;
#Parsent = 1;

title "処理中．．．0 %";
while (#Total >= #Count) {
	if ((#Count * 100 / #Total) > #Parsent) {
		title "処理中．．．" + str(#Parsent) + " %";
		#Parsent = #Parsent + 1;
	}
	$buffer = gettext(0, y, linelen2, y);
	if (strstr($buffer, "X") > -1 || strstr($buffer, "X") > -1) {
		if (strstr($buffer, "M05") > -1 || strstr($buffer, "M07") > -1) {
			deleteline;
			$buffer = dllfuncstr("GSUB", $buffer, "M05", "", -1);
			$buffer = dllfuncstr("GSUB", $buffer, "M07", "", -1);
			if (#HitFlag == 0) insert "G81\n";
			insertfix $buffer + "\n";
			#HitFlag = 1;
		} else if (#HitFlag == 1) {
			insert "G80\n";
			#HitFlag = 0;
		}
	}
	down;
	#Count = #Count + 1;
}
title "処理中．．．100 %";
save;
moveto #x, #y; // カーソルを元の位置に戻す
enabledraw;
title 0;
freedll;
