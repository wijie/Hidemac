//-------------------------------------------------------//
// NcConv.mac -- NCデータ変換 User Interface Macro       //
// Ver0.24                                               //
//                                                       //
// Author:  WATABE Eiji                                  //
// Created: Jan 18, 2000                                 //
// Revised: May 28, 2001                                 //
//-------------------------------------------------------//

// 田楽DLLのロード
freedll;
loaddll(hidemarudir + "\\DengakuDLL.dll");
if (!result) {
	message "DLL をロードできませんでした。";
	endmacro;
}

$temp = getenv("temp");
if (rightstr($temp, 1) != "\\") $temp = $temp + "\\";
$cfg = $temp + "ncconv.cfg";
$nc_tmp = $temp + "ncconv.$nc";
$outfile = $temp + "ncconv.out";
$hidden = hidemarudir + "\\hidden.exe";
// 変換エンジンのコマンド
$command = $hidden + " ruby c:\\usr\\local\\share\\ruby\\ncconv.rb " + $cfg;
// サブメモリを展開するコマンド
$ExtractCmd = $hidden + " gawk -f extract.awk ";

// メインメニュー
if (dllfunc("NEWMENU", "main") == 0 ||
	dllfunc("ADDMENUITEM", "", "ABSに変換", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "INCに変換", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "オフセット", "OffSet") == 0 ||
	dllfunc("ADDMENUITEM", "", "回転", "Rotate") == 0 ||
	dllfunc("ADDMENUITEM", "", "ミラー", "Mirror") == 0 ||
	dllfunc("ADDMENUITEM", "", "スケーリング", "Scale") == 0 ||
	dllfunc("ADDMENUITEM", "", "ﾘｰﾃﾞｨﾝｸﾞｾﾞﾛ → ﾄﾚｰﾃﾞｨﾝｸﾞｾﾞﾛ", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "", "") == 0 || // セパレータ
	dllfunc("ADDMENUITEM", "", "終了", "") == 0) {
	message "main メニューの作成に失敗しました。";
	goto end;
}

// オフセットのサブメニュー
if (dllfunc("NEWMENU", "OffSet") == 0 ||
	dllfunc("ADDMENUITEM", "", "ABS", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "INC", "") == 0) {
	message "comdlg メニューの作成に失敗しました。";
	goto end;
}

// 回転のサブメニュー
if (dllfunc("NEWMENU", "Rotate") == 0 ||
	dllfunc("ADDMENUITEM", "", "90°", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "180°", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "270°", "") == 0) {
	message "comdlg メニューの作成に失敗しました。";
	goto end;
}

// ミラーのサブメニュー
if (dllfunc("NEWMENU", "Mirror") == 0 ||
	dllfunc("ADDMENUITEM", "", "X軸を中心に反転", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "Y軸を中心に反転", "") == 0) {
	message "comdlg メニューの作成に失敗しました。";
	goto end;
}

// スケーリングのサブメニュー
if (dllfunc("NEWMENU", "Scale") == 0 ||
	dllfunc("ADDMENUITEM", "", "ABS", "") == 0 ||
	dllfunc("ADDMENUITEM", "", "INC", "") == 0) {
	message "comdlg メニューの作成に失敗しました。";
	goto end;
}
// メニューの表示（現在のカーソル位置に表示）
$item = dllfuncstr("MENU", "main", hidemaruhandle(0));
// メニューの表示（現在のマウスカーソル位置に表示）
//$item = dllfuncstr("MENU", "main", 0);

if ($item == "!") goto end;
else if ($item == "0" || $item == "main:9") goto end; // マクロの終了
else if ($item == "main:1") $ConvTo = "ABS";
else if ($item == "main:2") $ConvTo = "INC";
else if (leftstr($item, 6) == "OffSet") {
	title "オフセット";
	$ConvTo = "OffSet";
	##i = val(midstr($item, 7, 2));
	if (##i == 1) $DataType = "ABS";
	else if (##i == 2) $DataType = "INC";

	offset_x:
	$offset_x = input("Xのオフセット量は?");
	if (result == 0) goto end;
	call inputchk $offset_x;
	if (##return == -1) goto offset_x;

	offset_y:
	$offset_y = input("Yのオフセット量は?");
	if (result == 0) goto end;
	call inputchk $offset_y;
	if (##return == -1) goto offset_y;
} else if (leftstr($item, 6) == "Rotate") {
	##i = val(midstr($item, 7, 2));
	if (##i == 1) $ConvTo = "R90";
	else if (##i == 2) $ConvTo = "R180";
	else if (##i == 3) $ConvTo = "R270";
} else if (leftstr($item, 6) == "Mirror") {
	##i = val(midstr($item, 7, 2));
	if (##i == 1) $ConvTo = "X_Mir";
	else if (##i == 2) $ConvTo = "Y_Mir";
} else if (leftstr($item, 5) == "Scale") {
	title "スケーリング";
	$ConvTo = "Scalling";
	##i = val(midstr($item, 6, 2));
	if (##i == 1) $DataType = "ABS";
	else if (##i == 2) $DataType = "INC";

	factor_x:
	$factor_x = input("XのFactorは?");
	if (result == 0) goto end;
	call inputchk $factor_x;
	if (##return == -1) goto factor_x;

	factor_y:
	$factor_y = input("YのFactorは?", $factor_x);
	if (result == 0) goto end;
	call inputchk $factor_y;
	if (##return == -1) goto factor_y;

	origin:
	$origin = input("基点の座標は?", "0,0");
	if (result == 0) goto end;
	$org_x = dllfuncstr("GETTOKEN", $origin, ",");
	call inputchk $org_x;
	if (##return == -1) goto origin;
	$org_y = dllfuncstr("GETTOKEN", "", ",");
	call inputchk $org_y;
	if (##return == -1) goto origin;
} else if ($item == "main:7") {
	digit:
	$digit = input("桁数は?", "3.2");
	if (result == 0) goto end;
	call inputchk $digit;
	if (##return == -1) goto digit;
	$ConvTo = "LZ2TZ";
}

title "変換中．．．";
#x = x;
#y = y;
disableinvert;
disabledraw;
if (selecting == 0) selectall;
cut;
#handle1 = hidemaruhandle(0);
openfile "/h " + $nc_tmp;
#handle2 = hidemaruhandle(0);
selectall;
delete;
paste;
save;
setactivehidemaru #handle1;
closehidemaru #handle2;
if ($ConvTo == "Scalling") {
	// サブメモリを展開する
	runsync2 $ExtractCmd + $nc_tmp + " > " + $temp + "ncconv.1";
	$nc_tmp = $temp + "ncconv.1";

	if ($origin != "0,0") {
		// オフセットをかける
		$ConvTo = "OffSet";
		// 入力文字の先頭が"-"の時"-"を消す, "-"でない時"-"を付ける
		// (整数版秀丸では小数点が丸められるので"-1"を掛けて符号を反転してはダメ)
		if (ascii($org_x) == 45)
			$offset_x = rightstr($org_x, strlen($org_x) - 1);
		else
			$offset_x = "-" + $org_x;
		if (ascii($org_y) == 45)
			$offset_y = rightstr($org_y, strlen($org_y) - 1);
		else
			$offset_y = "-" + $org_y;
		call save_cfg;
		runsync2 $command + " > " + $temp + "ncconv.2";

		// スケーリングをかける
		$ConvTo = "Scalling";
		$offset_x = "";
		$offset_y = "";
		$nc_tmp = $temp + "ncconv.2";
		call save_cfg;
		runsync2 $command + " > " + $temp + "ncconv.3";

		// 原点を元に戻す
		$ConvTo = "OffSet";
		$offset_x = $org_x;
		$offset_y = $org_y;
		$nc_tmp = $temp + "ncconv.3";
	}
}
call save_cfg;
runsync2 $command + " > " + $outfile;
insertfile $outfile;
save;
// カーソルを元の位置に戻す
moveto #x, #y;
#a = dllfunc("REMOVE", $temp + "ncconv.*");

//-------------------------------------------------------//
// 終了処理                                              //
//-------------------------------------------------------//
end:
	title 0;
	freedll;
	endmacro;

//-------------------------------------------------------//
// 入力文字のチェック                                    //
//-------------------------------------------------------//
inputchk:
	if ($$1 == "b" || $$1 == "B")
		return -2;
	// 入力文字の先頭が0〜9以外の時エラーにする。
	else if (ascii($$1) < 48 || ascii($$1) > 57 && ascii($$1) != 45) {
		message "数値が無効です。 → " + $$1;
		return -1;
	} else
		return 1;

//-------------------------------------------------------//
// ncconv.cfgの保存                                      //
//-------------------------------------------------------//
save_cfg:
	// 存在しているファイルをopenfile " /h"すると
	// Windows2000でエラーになる時があるので消しておく
	##a = dllfunc("REMOVE", $cfg);
	##handle1 = hidemaruhandle(0);
	openfile "/h " + $cfg;
	##handle2 = hidemaruhandle(0);
	insert "DataType=" + $DataType + "\n";
	insert "ConvTo=" + $ConvTo + "\n";
	insert "ncdata=" + $nc_tmp + "\n";
	insert "Digit=" + $digit + "\n";
	if ($offset_x != "" || $offset_y != "")
		insert "OffSet=" + $offset_x + "," + $offset_y + "\n";
	else if ($factor_x != "" || $factor_y != "")
		insert "Factor=" + $factor_x + "," + $factor_y + "\n";
	config "m-";
	save;
	setactivehidemaru ##handle1;
	closehidemaru ##handle2;
	return;
